plugins {
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
    id 'org.openapi.generator' version '6.6.0'
}

group = 'com.softarc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'com.microsoft.playwright:playwright:1.27.1'
  testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
}


tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
  generatorName.set('spring')
  inputSpec.set("$rootDir/api/bookings.yml")
  outputDir.set("$rootDir/generated")
  additionalProperties.set([
    interfaceOnly: true,
    apiPackage: 'com.softarc.eternal.web.api',
    documentationProvider: 'none',
    modelPackage: 'com.softarc.eternal.web.model',
    openApiNullable: false,
    useSpringBoot3: true,
    useBeanValidation: true,
    useTags: true
  ])
}

sourceSets.main.java.srcDirs += 'generated/src/main/java'
